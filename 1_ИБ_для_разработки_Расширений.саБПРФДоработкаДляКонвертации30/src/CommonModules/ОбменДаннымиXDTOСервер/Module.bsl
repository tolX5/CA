
//тол+
// Добавлена возможность подключения правил обмена через внешнюю обработку в плане обмена.
// в типовой БП, как и в БСП возможность отсутствует
//

Процедура EDм_ПолучитьВерсиюФорматаОбмена(ВерсииФормата, Знач УзелИнформационнойБазы) 
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СинхронизацияДанныхЧерезУниверсальныйФормат.ПутьКМенеджеруОбмена КАК ПутьКМенеджеруОбмена,
	|	СинхронизацияДанныхЧерезУниверсальныйФормат.ВерсияФорматаОбмена КАК ВерсияФорматаОбмена
	|ИЗ
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК СинхронизацияДанныхЧерезУниверсальныйФормат
	|ГДЕ
	|	СинхронизацияДанныхЧерезУниверсальныйФормат.ПутьКМенеджеруОбмена <> """"
	|	И СинхронизацияДанныхЧерезУниверсальныйФормат.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", УзелИнформационнойБазы);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ИмяОбработки = Выборка.ПутьКМенеджеруОбмена;
		
		Если НЕ ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда 
			ДанныеОбработки = Новый ДвоичныеДанные(ИмяОбработки);
			АдресОбработки  = ПоместитьВоВременноеХранилище(ДанныеОбработки);
			
			Если ОбщегоНазначения.ЕстьЗащитаОтОпасныхДействий() Тогда
				ИмяОбработки = ВнешниеОбработки.Подключить(АдресОбработки, , ,
				ОбщегоНазначения.ОписаниеЗащитыБезПредупреждений());
			Иначе
				ИмяОбработки = ВнешниеОбработки.Подключить(АдресОбработки);
			КонецЕсли;
		КонецЕсли;
		
		МенеджерОбмена = ВнешниеОбработки.Создать(ИмяОбработки);
		
		ВерсииФормата.Вставить(Выборка.ВерсияФорматаОбмена, МенеджерОбмена);
		
	КонецЦикла;
	
КонецПроцедуры

&Вместо("ВерсииФорматаОбмена")
Функция EDм_ВерсииФорматаОбмена(Знач УзелИнформационнойБазы)
	
	ВерсииФорматаОбмена = Новый Соответствие;
	
	Если ЗначениеЗаполнено(УзелИнформационнойБазы) Тогда
		ИмяПланаОбмена = УзелИнформационнойБазы.Метаданные().Имя;
		ВерсииФорматаОбмена = ОбменДаннымиСервер.ЗначениеНастройкиПланаОбмена(ИмяПланаОбмена,"ВерсииФорматаОбмена");
		EDм_ПолучитьВерсиюФорматаОбмена(ВерсииФорматаОбмена, УзелИнформационнойБазы);
	Иначе
		ОбменДаннымиПереопределяемый.ПриПолученииДоступныхВерсийФормата(ВерсииФорматаОбмена);
	КонецЕсли;
	
	Если ВерсииФорматаОбмена.Количество() = 0 Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не заданы версии формата обмена.
			|Имя плана обмена: %1
			|Процедура: ПолучитьВерсииФорматаОбмена(<ВерсииФорматаОбмена>)'"),
			УзелИнформационнойБазы.Метаданные().Имя);
	КонецЕсли;
	
	Результат = Новый Соответствие;
	
	Для Каждого Версия Из ВерсииФорматаОбмена Цикл
		
		Результат.Вставить(СокрЛП(Версия.Ключ), Версия.Значение);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции